---
title: 文本函数
---

## BEGINS

确定文本是否以特定字符开头，如果是则返回 TRUE（真）。如果不是，则返回 FALSE（假）。

**使用：** BEGINS(文本, 比较文本)

**参数：**

- *文本:* `text`
您希望进行比较的字符全文或字段。
- *比较文本:* `text`
用于比较的文本片断。

**返回值：** 布尔

```js
IF(BEGINS (Product_type__c , “ICU”), “Medical”, “Technical”)
```

如果产品类型自定义文本字段中的文本以“ICU”开头，则此示例返回文本“Medical”（医疗）。对于所有其他产品显示“Technical”（技术）。

:::note 技巧提示

- ■ 此函数区分大小写，因此，请确保比较文本值的大小写正确无误。
- ■ 在验证规则或工作流规则中使用此函数时，留空的字段被视为有效。例如，如果您的验证规则要测试资产序列号是否以“3”开始，则序列号为空的所有资产都将视为有效。
:::

## BR

在文本字符串中插入一个换行符。

**使用：** BR()

**返回值：** 文本

```js
CASE(ShippingCountry,
"USA",
  ShippingStreet & BR() &
  ShippingCity & ",
  " & ShippingState & " " &
  ShippingPostalCode & BR()
  & ShippingCountry,
"France",
  ShippingStreet & BR() &
  ShippingPostalCode & " " &
  ShippingCity & BR() &
  ShippingCountry, "etc")
```

此公式字段以标准格式显示联系人的带格式邮寄地址，根据所在的国家/地区，可能会在该地址的适当位置中包含空格和换行符。

:::note 技巧提示

- ■ 请勿删除函数名称后面的括号。
- ■ 保留括号为空。括号中不必包含值。
- ■ 请勿忘记在 BR() 两侧加上串联运算符：&。
:::

## CASESAFEID

将 15 个字符的 ID 转换成区分大小写的 18 个字符 ID。

**使用：** CASESAFEID(id)

**参数：** `text`
对象 ID。

**返回值：** 文本

```js
CASESAFEID (Id)
```

此公式将 15 个字符的 ID 替换为 18 个字符、区分大小写的 ID。
假设Id值为abcdefghijklmno，该公式将返回"abcdefghijklmnoAAA"。

:::note 技巧提示

- ■ 请勿删除函数名称后面的括号。
- ■ 保留括号为空。括号中不必包含值。
- ■ 请勿忘记在 BR() 两侧加上串联运算符：&。
:::

## CONTAINS

比较两个文本参数，如果第一个参数包含第二个参数，则返回 TRUE（真）。如果不包含，则返回 FALSE（假）。

**使用：** CONTAINS(文本, 比较文本)

**参数：**

- *文本:* `text`
原始文本全文。
- *比较文本:* `text`
用于比较的文本片断。

**返回值：** 布尔

```js
IF(CONTAINS(Product_Type__c, "part"), "Parts", "Service")
```

此公式检查自定义文本字段 Product_Type（产品类型）的内容，并针对任何包含单词“part”的产品返回“Parts”。否则，将返回“Service”。请注意，如果 Product_Type（产品类型）字段包含“Part”或“PART”文本，此公式将返回“Services”。

:::note 技巧提示

- ■ 此函数区分大小写，因此，请确保比较文本值的大小写正确无误。
- ■ 在验证规则或工作流规则中使用此函数时，留空的字段被视为有效。例如，如果您的验证规则用来测试资产序列号是否包含“A”，那么，所有具有空序列号的资产将被视为有效。
- ■ CONTAINS 函数不支持多选选项列表。
:::

## FIND

返回某个字符串在文本字符串内的位置（以数字形式表示）。

**使用：** FIND(搜索文本, 文本[, 起始字符数])

**参数：**

- *搜索文本:* `text`
要查找的字符串。
- *文本:* `text`
要搜索的字段或表达式。
- *起始字符数:* `number`
要从左边算起的作为搜索起始位置的字符数。

**返回值：** 数值

```js
FIND(" ", Street)
```

返回 Street 字段中的第一个空格所在的字符位置。可以使用此数字来确定街道地址的长度，以便将地址字段中的街道地址与街道名称分开。

```js
SUBSTITUTE(Email, LEFT(Email, FIND("@", Email)), "www.")
```

查找 @ 符号在个人电子邮件地址中的位置，以确定要用“www.”替换的文本长度，从而派生其网址。

:::note 技巧提示

- ■ 请确保在验证公式之前去掉其中的中括号、[ 和 ]。
- ■ 如果文本参数中引用的字段为空，则该公式字段显示“0”。
- ■ 您的 search_text 参数区分大小写，且不能包括任何通配符。
- ■ 如果在执行搜索后未返回任何结果，则字段中将显示“0”。
- ■ 起始字符数参数是可选的。如果未输入起始字符数 值，则该公式将使用值 1 或者字符串中的第一个字符。
- ■ 如果起始字符数不大于零，则该字段中将显示“0”。
- ■ 如果起始字符数大于文本的长度，则该字段中将显示“0”。
- ■ 在输入起始字符数参数时，请记住，一些字段（如网址字段）是唯一的，因为系统会自动在您输入的文本开头附加“http://”。
- ■ 请注意，字符串中的第一个字符指定为 1（而非 0）。
:::

## HYPERLINK

从指定文本创建一个到可链接的指定 URL 的链接。

**使用：** HYPERLINK(url, friendly_name [,target])

**参数：**

**返回值：** 文本

- *url:* `text`
网址。
- *friendly_name:* `text`
链接文本。
- *target:* `text`
要在其中显示内容的窗口或帧。

```js
HYPERLINK("/00U/e?
retURL=%2F006x0000001T8Om&what_id="
& Id,
"Create Event")
```

添加一个名为“Create Event”（创建事件）的链接，当用户单击该链接时，系统会创建一个与当前的目标相关联的新事件。

```js
HYPERLINK("http://servername/call?id=" & Id & "&phone=" & Phone, Phone)
```

创建一个可链接的电话号码字段，当用户单击该字段时，系统会自动拨打其中的电话号码。在本示例中，用您的拨号工具名称及其用来拨号的命令替换“servername”（服务器名）和“call”（呼叫）。并在合并字段 Id 中插入联系人、业务机会或客户记录的标识符。第一个 Phone（电话）合并字段通知拨号工具要拨打的号码，最后一个 Phone（电话）合并字段使用 Phone（电话）字段的值作为用户单击时拨号的可链接文本。

:::note 技巧提示

- ■ 超链接公式字段属于文本类型。
- ■ 包含如 `HYPERLINK("http://www.cnet.com", "cnet")` 中引号内的协议和 URL。
- ■ 避免在 HYPERLINK 函数结果中使用文本函数，如 LEN、LEFT 或 RIGHT。
- ■ URL 不得包含 JavaScript。这会提高贵组织的安全性。
- ■ 使用相对链接，以链接到站内页面。如果您的完整链接是 `https://yourInstance.steedos.com/a/b`，相对链接是“/a/b”。通过相对链接，超链接可在所有站内页面上正常工作。请确保使用斜杠“/”在相对 URL 前增加。
- ■ 如果起始字符数不大于零，则该字段中将显示“0”。
- ■ 如果起始字符数大于文本的长度，则该字段中将显示“0”。
- ■ 在输入起始字符数参数时，请记住，一些字段（如网址字段）是唯一的，因为系统会自动在您输入的文本开头附加“http://”。
- ■ 华炎魔方在界面上暂时不支持把该函数返回值显示为链接。
:::

## LEFT

返回从文本字符串开头算起的指定数量的字符。

**使用：** LEFT(文本, 字符数)

**参数：**

- *文本:* `text`
您希望返回的字段或表达式。
- *字符数:* `number`
您希望返回的从左边算起的字符数。

**返回值：** 文本

```js
TRIM(LEFT(LastName, 5)) & “-” & TRIM(RIGHT(SSN__c, 4))
```

此公式显示名称的前五个字符和社会保险编号的后四个字符（由连字符分隔）。请注意，本示例使用了名为 SSN 的自定义文本字段。

:::note 技巧提示

- 如果字符数值小于零，公式会用零替换该值。
:::

## LEN

返回指定的文本字符串中所含的字符数。

**使用：** LEN(文本)

**参数：** `text`
您希望返回其长度的字段或表达式。

**返回值：** 数值

```js
LEN(PartNumber__c)
```

此公式返回 Product Code（产品代码）字段中所含的字符数。

## LOWER

将指定文本字符串中的所有字母转换为小写。不是字母的任何字符不受此函数的影响。如果提供区域则应用区域规则。

**使用：** LOWER(text)

**参数：** `text`
您要转换为小写的字段或文本。

**返回值：** 文本

```js
LOWER("MYCOMPANY.COM")
```

返回“mycompany.com.”。

```js
LOWER( TickerSymbol )
```

以小写字符返回 Ticker Symbol 中的文本。

## LPAD

将指定的字符插入文本字符串的左侧。

**使用：** LPAD(文本, 填充长度, 填充字符串)

**参数：**

- *文本:* `text`
文本是要在其左侧插入字符的字段或表达式。
- *填充长度:* `number`
填充长度是文本中将返回的总字符数。
- *填充字符串:* `text`
填充字符串是应插入的字符。填充字符串是必须的，可以传入空字符串表示不填充。

**返回值：** 文本

***示例：***

***1.字段名：截断***

如果文本中值的长度超过了填充字符串，则将文本截断为填充长度的大小。

```js
LPAD(Name, 20, "")
```

在 20 个字符后截断名称字段。例如，如果初始值为"mycompany.com"，则返回值为"mycompany.com"，如果初始值为"mycompany_is_good_in_china.com"，则返回值为"mycompany_is_good_in"。

***2.My_Company: 无更改***

```js
LPAD( 'my_company.com' , 14, 'z')
```

返回“my_company.com”而没有更改，因为它包含 14 个字符。

***3.使用 Z 填充的字段名***

```js
LPAD("my_company.com", 15, "z")
```

返回名称"zmy_company.com"。

:::note 技巧提示
不会忽略前置空格。
:::

## MID

从文本字符串中间返回从给定的起始位置算起的指定数量的字符。

**使用：** MID(文本, 起始字符数, 字符数)

**参数：**

- *文本:* `text`
返回字符时要使用的字段或表达式。
- *起始字符数:* `number`
从左边算起的作为起始位置的字符数（作为起始位置的字符数）。
- *字符数:* `number`
要返回的总字符数。

**返回值：** 文本

```js
MID( Division , 3, 4)
```

返回 Division（分部）名称中从左数第三个字符开始的四个字符。对于用户记录，这代表部门代码。

```js
MID("ABCDEFGH" , 3, 4)
```

返回"CDEF"。

```js
MID("ABCDEFGH", 0, 10)
```

返回"H"，即起始位置是从0开始而非1，传入0值作为起始位置时公式将不能按预期返回值。

:::note 技巧提示

- 请注意，文本中的第一个字符指定为 1（而非 0）。
:::

## RIGHT

返回从文本字符串右边算起的指定数量的字符。

**使用：** RIGHT(文本, 字符数)

**参数：**

- *文本:* `text`
您希望返回的字段或表达式。
- *字符数:* `number`
您希望返回的从右边算起的字符数。

**返回值：** 文本

```js
TRIM(LEFT(LastName, 5))&"-"&TRIM(RIGHT(SSN__c, 4))
```

显示名字的前五个字符和社会保险编号的后四个字符（由连字符分隔）。请注意，本示例假定您拥有名为 SSN 的文本自定义字段。

:::note 技巧提示
如果字符数值小于零，则公式会用零替换该值。
:::

## RPAD

将指定的字符插入文本字符串的右侧。

**使用：** RPAD(文本, 填充长度, 填充字符串)

**参数：**

- *文本:* `text`
文本是要在其后插入字符的字段或表达式。
- *填充长度:* `number`
填充长度是文本中将返回的总字符数。
- *填充字符串:* `number`
填充字符串是应插入的字符。填充字符串是必须的，可以传入空字符串表示不填充。
如果文本中值的长度超过了填充字符串，则将文本截断为填充长度的大小。

**返回值：** 文本

***示例：***

***1.字段名：截断***

```js
RPAD(Name, 20, "")
```

在 20 个字符后截断名称字段。例如，如果初始值为"mycompany.com"，则返回值为"mycompany.com"，如果初始值为"mycompany_is_good_in_china.com"，则返回值为"mycompany_is_good_in"。

***2.My_Company: 无更改***

```js
RPAD( 'my_company.com' , 14, 'z')
```

返回“my_company.com”而没有更改，因为它包含 14 个字符。

***3.使用 Z 填充的字段名***

```js
RPAD("my_company.com", 15, "z")
```

返回名称"my_company.comz"。

:::note 技巧提示

- ■ 不会忽略结尾空格。
- ■ 在截断字符时与函数LPAD一样从右侧开始截断保留左侧部分，而不是相反。
:::

## SUBSTITUTE

用新文本替换文本字符串中的旧文本。

**使用：** SUBSTITUTE(文本, 旧文本, 新文本)

**参数：**

- *文本:* `text`
要进行值替换的字段或值。
- *旧文本:* `text`
要被替换的文本。
- *新文本:* `text`
要用来替换旧文本的文本。

**返回值：** 文本

```js
SUBSTITUTE(Name, "Coupon", "Discount")
```

返回包含术语“Coupon”（赠券）的业务机会的名称，返回名称时，将名称中的“Coupon”替换为“Discount”。

```js
SUBSTITUTE(Email, LEFT(Email, FIND("@", Email)), "www.")
```

查找 @ 符号在个人电子邮件地址中的位置，以确定要用“www.”替换的文本长度，从而派生其网址，比如Email值为"abc@steedos.com"的话运行公式将返回"www.steedos.com"。

:::note 技巧提示

- ■ 引号中的每个术语都区分大小写。
- ■ 如果旧文本出现多次，则所出现的每个“旧文本”都将用所提供的新文本值替换，即使这会导致重复项也是如此。
:::

## TEXT

在使用公式的任何地方将百分比、数字、日期、日期时间或货币类型字段转换为文本。

**使用：** TEXT(值)

**参数：** `text`
您希望转换为文本格式的字段或表达式。避免在此函数中使用除小数点（句点）或负号（连字符）以外的任何特殊字符。

**返回值：** 文本

```js
TEXT(ExpectedRevenue)
```

以文本格式返回业务机会的预期收入金额。例如，如果市场活动的 Expected Revenue（预期收入）是200000，此公式的字段中将显示“200000”。

```js
SerialNumber &"-"& TEXT(Quantity)
```

返回一个资产 ID 号，ID 号以序列号开头，以数量结尾，二者之间用连字符分隔。Serial Number（序列号）字段是文本字段，但 Quantity（数量）字段为数字字段，其前面需要使用 TEXT 函数。

:::note 技巧提示

- ■ 返回的文本未使用任何货币、百分比符号或逗号进行自动格式处理。
- ■ 百分数以小数形式返回。
- ■ 日期以 YYYY-MM-DD 格式（即四位数字年份、两位数字月份和两位数字日期）返回。
- ■ 日期时间值以 YYYY-MM-DD HH:MM:SSZ 格式返回，其中 YYYY 是四位数字年份，MM 是两位数字月份，DD 是两位数字日期，HH 是两位数字小时，MM 是两位数字分钟，SS 是两位数字秒钟，Z 代表本初子午线，指示时间以 UTC 时区返回。
:::

## TRIM

删除文本字符串开头和末尾处的空格和制表符。

**使用：** TRIM(文本)

**参数：** `text`
用您希望修整的字段或表达式替换文本。

**返回值：** 文本

```js
TRIM(LEFT(LastName,5))& "-" & RIGHT(FirstName, 1)
```

为用户返回一个网络 ID，该 ID 用连字符分隔其姓氏的前五个字符和其名字的第一个字符。

## UPPER

将指定文本字符串中的所有字母转换为大写。不是字母的任何字符不受此函数的影响。如果提供区域则应用区域规则。

**使用：** UPPER(text)

**参数：** `text`
将 text 用您要转换为大写的字段或表达式替换。

**返回值：** 文本

```js
UPPER("mycompany.com")
```

返回“MYCOMPANY.COM.”。

```js
UPPER("Mycompany.com 123")
```

返回“MYCOMPANY.COM 123.”。

## VALUE

将文本字符串转换为数字。

**使用：** VALUE(文本)

**参数：** `text`
您希望转换为数字的字段或表达式。

**返回值：** 数值

***示例：***

***1.潜在客户编号***

```js
VALUE( Lead_Number__c )
```

返回自动编号字段 Lead Number（潜在客户编号）中的文本值所对应的数字。如果您希望在计算中使用 Lead Number（潜在客户编号）字段，则这可能非常有用。请注意，自动编号字段实际上是文本字段，必须转换为数字字段才能进行数值计算。

***2.潜在客户循环分配***

```js
MOD(VALUE(Lead_Number__c), 3)
```

该公式适用于名称为 Round_Robin_ID 的自定义公式字段，它为每个潜在客户分配 0、1 或 2。该公式使用名称为 Lead Number 的自定义自动数字字段，它为每个潜在客户分配从 1 开始的唯一号码。MOD 函数使用潜在客户数量除以可用潜在客户队列的数量（在本示例中为 3），然后返回剩余值 0、1 或 2。在您的潜在客户分配规则中使用该公式字段的值向不同的队列分配潜在客户记录。例如：

- Round_Robin_ID = 0 分配到队列 A
- Round_Robin_ID = 1 分配到队列 B
- Round_Robin_ID = 2 分配到队列 C

:::note 技巧提示
请确保 VALUE 函数中的文本不包含除小数点（句点）或负号（连字符）以外的任何特殊字符。例如，公式 VALUE(Text_field__c) 产生以下结果：

- ■ 如果文本字段为123，则结果为 123。
- ■ 如果文本字段为0，则返回错误信息。
- ■ 如果文本字段为空白，则返回错误信息。
- ■ 如果文本字段为$123，则返回错误信息。
:::
